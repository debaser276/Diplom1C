Функция РеализацииЗаПериод(Знач Период) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДоговорыКонтрагентов.Ссылка КАК Ссылка,
		|	ДоговорыКонтрагентов.Организация,
		|	ДоговорыКонтрагентов.Владелец
		|ПОМЕСТИТЬ вт_Договоры
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|ГДЕ
		|	ДоговорыКонтрагентов.ВидДоговора = &ВидДоговора
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РеализацияТоваровУслуг.Договор КАК Договор,
		|	РеализацияТоваровУслуг.Ссылка
		|ПОМЕСТИТЬ вт_Документы
		|ИЗ
		|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|ГДЕ
		|	МЕСЯЦ(РеализацияТоваровУслуг.Дата) = МЕСЯЦ(&Период)
		|	И ГОД(РеализацияТоваровУслуг.Дата) = ГОД(&Период)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Договор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_Договоры.Ссылка КАК Договор,
		|	вт_Договоры.Владелец КАК Контрагент,
		|	вт_Договоры.Организация КАК Организация
		|ИЗ
		|	вт_Договоры КАК вт_Договоры
		|		ЛЕВОЕ СОЕДИНЕНИЕ вт_Документы КАК вт_Документы
		|		ПО вт_Договоры.Ссылка = вт_Документы.Договор.Ссылка
		|ГДЕ
		|	вт_Документы.Ссылка ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.АбонентскоеОбслуживание);
	Запрос.УстановитьПараметр("Период", Период);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	ДокументыРеализации = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		
		НовыйДокумент = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
		НовыйДокумент.Дата = КонецМесяца(Период);
		НовыйДокумент.Договор = Выборка.Договор;
		НовыйДокумент.Контрагент = Выборка.Контрагент;
		НовыйДокумент.Организация = Выборка.Организация;
		НовыйДокумент.Ответственный = Пользователи.ТекущийПользователь();
		НовыйДокумент.ВыполнитьАвтозаполнение();
		
		НовыйДокумент.Записать();
		СтрокаТаблицы = Новый Структура;
		СтрокаТаблицы.Вставить("Договор", Выборка.Договор);
		СтрокаТаблицы.Вставить("ДокументРеализации", НовыйДокумент.Ссылка);
		ДокументыРеализации.Добавить(СтрокаТаблицы);
		
	КонецЦикла;
	
	Возврат ДокументыРеализации;
	
КонецФункции