#Область СлужебныеПроцедурыИФункции

Процедура РассчитатьНачисления(Регистратор, Движения) Экспорт
	
	РассчитатьОсновныеНачисления(Регистратор, Движения.ВКМ_ОсновныеНачисления);
	РассчитатьДополнительныеНачисления(Регистратор, Движения.ВКМ_ДополнительныеНачисления);
	РассчитатьУдержания(Регистратор, Движения);
	
КонецПроцедуры

Процедура РассчитатьОсновныеНачисления(Регистратор, ОсновныеНачисления)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки,
		|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияДанныеГрафика.ДнейПериодДействия, 0) КАК План,
		|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияДанныеГрафика.ДнейФактическийПериодДействия, 0) КАК Факт,
		|	ВКМ_ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник
		|ПОМЕСТИТЬ вт_Документ
		|ИЗ
		|	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(Регистратор = &Регистратор
		|	И ВидРасчета = &ВидРасчетаОклад) КАК ВКМ_ОсновныеНачисленияДанныеГрафика
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_Документ.НомерСтроки,
		|	вт_Документ.План,
		|	вт_Документ.Факт,
		|	ЕСТЬNULL(ВКМ_УсловияОплатыТрудыСрезПоследних.Оклад, 0) КАК Оклад
		|ИЗ
		|	вт_Документ КАК вт_Документ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыТруды.СрезПоследних(&ДатаСреза, Сотрудник В
		|			(ВЫБРАТЬ
		|				Т.Сотрудник
		|			ИЗ
		|				вт_Документ КАК Т)) КАК ВКМ_УсловияОплатыТрудыСрезПоследних
		|		ПО вт_Документ.Сотрудник = ВКМ_УсловияОплатыТрудыСрезПоследних.Сотрудник";
	
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.УстановитьПараметр("ВидРасчетаОклад", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад);
	Запрос.УстановитьПараметр("ДатаСреза", НачалоМесяца(Регистратор.Дата));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Для Каждого Строка Из ОсновныеНачисления Цикл
		
		Если Строка.ВидРасчета <> ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад Тогда
			Продолжить;
		КонецЕсли;
		
		Выборка.Сбросить();
		Выборка.НайтиСледующий(Строка.НомерСтроки);
		Строка.Результат = ?(Выборка.План = 0, 0, Выборка.Оклад / Выборка.План * Выборка.Факт);	
		Строка.РезультатРабочихДней = Выборка.План;
		Строка.Параметр = Выборка.Факт;
			
	КонецЦикла;

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.РезультатБаза, 0) +
		|		ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления.РезультатБаза, 0) КАК База,
		|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.РезультатРабочихДнейБаза, 0) КАК РабочихДнейБаза
		|ПОМЕСТИТЬ вт_База
		|ИЗ
		|	РегистрРасчета.ВКМ_ОсновныеНачисления.БазаВКМ_ОсновныеНачисления(&Измерения, &Измерения,, Регистратор = &Регистратор
		|	И ВидРасчета = &ВидРасчетаОтпуск) КАК ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.БазаВКМ_ДополнительныеНачисления(&Измерения, &Измерения,,
		|			Регистратор = &Регистратор
		|		И ВидРасчета = &ВидРасчетаОтпуск) КАК ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления
		|		ПО ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.НомерСтроки = ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления.НомерСтроки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_База.НомерСтроки КАК НомерСтроки,
		|	вт_База.База КАК База,
		|	вт_База.РабочихДнейБаза КАК РабочихДнейБаза,
		|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияДанныеГрафика.ДнейФактическийПериодДействия, 0) КАК ДнейОтпуска
		|ИЗ
		|	вт_База КАК вт_База,
		|	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(Регистратор = &Регистратор
		|	И ВидРасчета = &ВидРасчетаОтпуск) КАК ВКМ_ОсновныеНачисленияДанныеГрафика";
	
	Измерения = Новый Массив;
	Измерения.Добавить("Сотрудник");
	
	Запрос.УстановитьПараметр("Измерения", Измерения);
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.УстановитьПараметр("ВидРасчетаОтпуск", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Для Каждого Строка Из ОсновныеНачисления Цикл
		
		Выборка.Сбросить();
		Если Выборка.НайтиСледующий(Строка.НомерСтроки) Тогда
			
			Строка.Результат = ?(Выборка.РабочихДнейБаза = 0, 0, (Выборка.База / Выборка.РабочихДнейБаза) * Выборка.ДнейОтпуска);
			Строка.РезультатДнейОтпуска = Выборка.ДнейОтпуска;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ОсновныеНачисления.Записать(, Истина);
	
КонецПроцедуры

Процедура РассчитатьДополнительныеНачисления(Регистратор, ДополнительныеНачисления)
	
	ПериодРегистрации = НачалоМесяца(Регистратор.Дата);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_ДополнительныеНачисления.Сотрудник КАК Сотрудник,
		|	ВКМ_ДополнительныеНачисления.НомерСтроки
		|ПОМЕСТИТЬ вт_Документ
		|ИЗ
		|	РегистрРасчета.ВКМ_ДополнительныеНачисления КАК ВКМ_ДополнительныеНачисления
		|ГДЕ
		|	ВКМ_ДополнительныеНачисления.Регистратор = &Регистратор
		|	И ВКМ_ДополнительныеНачисления.ВидРасчета = &ВидРасчетаПроцентОтРабот
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_Документ.НомерСтроки,
		|	ЕСТЬNULL(ВКМ_ВыполненныеСотрудникомРаботыОбороты.СуммаКОплатеОборот, 0) КАК СуммаКОплате
		|ИЗ
		|	вт_Документ КАК вт_Документ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВКМ_ВыполненныеСотрудникомРаботы.Обороты(&НачалоПериода, &КонецПериода,, Сотрудник
		|			В
		|			(ВЫБРАТЬ
		|				Т.Сотрудник
		|			ИЗ
		|				вт_Документ КАК Т)) КАК ВКМ_ВыполненныеСотрудникомРаботыОбороты
		|		ПО вт_Документ.Сотрудник = ВКМ_ВыполненныеСотрудникомРаботыОбороты.Сотрудник";
	
	Запрос.УстановитьПараметр("НачалоПериода", ПериодРегистрации);
	Запрос.УстановитьПараметр("ВидРасчетаПроцентОтРабот", ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.ПроцентЗаВыполненныеРаботы);
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(ПериодРегистрации));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Для Каждого Строка Из ДополнительныеНачисления Цикл
		
		Выборка.Сбросить();
		Если Выборка.НайтиСледующий(Строка.НомерСтроки) Тогда
			
			Строка.Результат = Выборка.СуммаКОплате;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ДополнительныеНачисления.Записать();
	
КонецПроцедуры

Процедура РассчитатьУдержания(Регистратор, Движения) 
	
	Движения.ВКМ_Удержания.Записывать = Истина;
	
	Для Каждого Строка Из Движения.ВКМ_ОсновныеНачисления Цикл
		ДобавитьУдержание(Строка, Движения.ВКМ_Удержания);
	КонецЦикла;
	
	Для Каждого Строка Из Движения.ВКМ_ДополнительныеНачисления Цикл
		ДобавитьУдержание(Строка, Движения.ВКМ_Удержания);
	КонецЦикла;
	
	Движения.ВКМ_Удержания.Записать();

КонецПроцедуры

Процедура ДобавитьУдержание(Строка, Удержания) Экспорт
	
	Движение = Удержания.Добавить();
	Движение.Сторно = Ложь;
	Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ;
	Движение.ПериодРегистрации = Строка.ПериодРегистрации;
	Движение.Сотрудник = Строка.Сотрудник;
	Движение.Результат = Строка.Результат * Константы.ВКМ_ПроцентНДФЛ.Получить() / 100;
	
КонецПроцедуры

#КонецОбласти










