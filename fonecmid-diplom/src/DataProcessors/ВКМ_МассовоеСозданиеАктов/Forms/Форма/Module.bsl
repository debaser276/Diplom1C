&НаКлиенте
Процедура Заполнить(Команда)

	Если Не ЗначениеЗаполнено(Объект.Период) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не установлен период";
		Сообщение.Поле = "Объект.Период";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперация = НачатьВыполнениеНаСервере();
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультат", ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);

КонецПроцедуры

&НаСервере
Функция НачатьВыполнениеНаСервере()
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	Возврат ДлительныеОперации.
		ВыполнитьФункцию(ПараметрыВыполнения, "ВКМ_МассовоеСозданиеРеализаций.РеализацииЗаПериод",
			Объект.Период);
КонецФункции

&НаКлиенте
Процедура ОбработатьРезультат(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Данные = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	
	Для Каждого Строка Из Данные Цикл
		
		НоваяСтрока = Объект.СписокДоговоров.Добавить();
		НоваяСтрока.Договор = Строка.Договор;
		НоваяСтрока.ДокументРеализации = Строка.ДокументРеализации;
		
	КонецЦикла;
	
КонецПроцедуры




















